/*
* Event Skript muss gestartet werden durch Event UI_COMMIT_COMPLETED (erst ab
* Junos ELS). Fuehrt Ping auf das Management-Netzwerk aus; beim Ping-Timeout wird
* ein automatischer Rollback auf die letzte Config-Revision durchgefuehrt.
*/

version 1.0;
ns junos = "http://xml.juniper.net/junos/*/junos";
ns xnm = "http://xml.juniper.net/xnm/1.1/xnm";
ns jcs = "http://xml.juniper.net/junos/commit-scripts/1.0";
import "../import/junos.xsl";


match / {
    <event-script-results> {
        call ping();
    }
}

template ping() {

	var $host = "10.1.1.1";

	var $ping-rpc = {
	    <ping> {
		    <host> $host;
		    <count> 5;
	    }
	}

	var $ping-out = jcs:invoke($ping-rpc);

	if ($ping-out/ping-success) {

		var $message = "Pingtest to Mgmt-Network after commit succeeded";
		expr jcs:syslog(2, $message);

	} else {

		var $message = "Pingtest to Mgmt-Network after commit failed; perform rollback!";
		expr jcs:syslog(2, $message);

		call rollback();
	}
}

template rollback() {

	expr jcs:syslog(2, "Begin auto-rollback");

	var $options := {
		<commit-options> {
			<log> "Rolled back configuration through SLAX event script";
        }
    }
	var $conn_handle = jcs:open();

	var $results := {
      	call jcs:load-configuration($commit-options=$options,
                $rollback=1, $connection=$conn_handle);
    }

	var $close-results = jcs:close($conn_handle);
}
